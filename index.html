<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta http-equiv="X-UA-Compatible" content="ie=edge">
    <link rel="stylesheet" href="./style/style.css">
    <title>Dithering</title>
</head>
<body>
    <div id="settings">
        
        <select name="picture" id="picture">
            <option value="./texture/david.png">David</option>
            <option value="./texture/lenna.png">Lenna</option>
            <option value="./texture/einstein.jpg">Einstein</option>
            <option value="./texture/bird.png">Bird</option>
            <option value="./texture/polygon.jpg">Polygons</option>
            <option value="./texture/scale.jpg">Scale</option>
            <option value="./texture/tex.jpg">me</option>
        </select>
        
        <select name="mode" id="mode">
            <option value="0">Normal</option>
            <option value="1">Grayscale</option>
            <option value="2">Threshold</option>
            <option value="3">Random</option>
            <option value="4">Bayer matrix 2x2</option>
            <option value="5">Bayer matrix 3x3</option>
            <option value="6">Bayer matrix 4x4</option>
            <option value="7">Bayer matrix 8x8</option>
            <option value="8">My matrix 5x5</option>
            <option value="9">Halftone</option>
        </select>
        
        <input type="range" list="tickmarks" value="40.0" id="range">

        <datalist id="tickmarks">
            <option value="0"></option>
            <option value="10"></option>
            <option value="20"></option>
            <option value="30"></option>
            <option value="40"></option>
            <option value="50"></option>
            <option value="60"></option>
            <option value="70"></option>
            <option value="80"></option>
            <option value="90"></option>
            <option value="100"></option>
        </datalist>
    </div>

    <canvas id="canvas"></canvas>

    <script id="vertexShader" type="x-shader/x-vertex">#version 300 es

        in vec2 inPosition;

        uniform mat3 uMatrix;

        out vec2 texCoord;
         
        void main() {
            float x = inPosition.x * 2.0 - 1.0;
            float y = inPosition.y * 2.0 - 1.0;

            texCoord = inPosition;
            gl_Position = vec4(uMatrix * vec3(x, y, 0.0), 1.0);
        }
    </script>

    <script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
        precision mediump float;

        in vec2 texCoord;

        uniform sampler2D tex;
        uniform int mode;
        uniform int range;

        const float m2x2[4] = float[] (
            0.0, 2.0,
            3.0, 1.0
        );

        const float m3x3[9] = float[] (
            0.0, 7.0, 3.0,
            6.0, 5.0, 2.0,
            4.0, 1.0, 8.0
        );

        const float m4x4[16] = float[](
            0.0,  8.0,  2.0,  10.0,
            12.0, 4.0,  14.0, 6.0,
            3.0,  11.0, 1.0,  9.0,
            15.0, 7.0,  13.0, 5.0
        );

        const float m5x5[25] = float[](
            0.0,  15.0, 3.0,  12.0,  8.0,
            6.0,  22.0,  14.0,  9.0, 13.0,
            3.0,  5.0,  1.0,  18.0, 7.0,
            8.0, 7.0,  20.0, 5.0,  16.0,
            9.0,  22.0, 17.0, 6.0, 24.0
        );

        const float m8x8[64] = float[](
             0.0, 32.0, 8.0,  40.0, 2.0,  34.0, 10.0, 42.0, 
            48.0, 16.0, 56.0, 24.0, 50.0, 18.0, 58.0, 26.0, 
            12.0, 44.0, 4.0,  36.0, 14.0, 46.0, 6.0,  38.0, 
            60.0, 28.0, 52.0, 20.0, 62.0, 30.0, 54.0, 22.0, 
             3.0, 35.0, 11.0, 43.0, 1.0,  33.0, 9.0,  41.0, 
            51.0, 19.0, 59.0, 27.0, 49.0, 17.0, 57.0, 25.0,
            15.0, 47.0, 7.0,  39.0, 13.0, 45.0, 5.0,  37.0,
            63.0, 31.0, 55.0, 23.0, 61.0, 29.0, 53.0, 21.0
        );

        const vec3 grayColor = vec3(0.299, 0.587, 0.114);

        out vec4 outColor;

        float random (vec2 vec) {
            return fract(sin(dot(vec.xy, vec2(12.9898, 78.233))) * 43758.5453123);
        }

        float grayscale () {
            vec4 col = texture(tex, texCoord);
            float gray = dot(col.rgb, grayColor);

            return gray;
        }

        int dither(float size) {
            int x = int(mod(gl_FragCoord.x, size));
            int y = int(mod(gl_FragCoord.y, size));

            float val = 0.0;
            if(size == 2.0) {
                val = m2x2[y + x * 2] / 4.0;

            } else if (size == 3.0) {
                val = m3x3[y + x * 3] / 9.0;

            } else if (size == 4.0) {
                val = m4x4[y + x * 4] / 16.0;

            } else if (size == 5.0) {
                val = m5x5[y + x * 5] / 25.0;

            } else if (size == 8.0) {
                val = m8x8[y + x * 8] / 64.0;
            }
            
            float col = grayscale();

            if(col > val) {
                return 1;
            } else {
                return 0;
            }
        }

        vec3 halftone() {
            vec4 texColor = texture(tex, texCoord);
            float r = float(range);

            vec2 nearest = 2.0 * fract(r * texCoord) - 1.0;
            float dist = length(nearest);
            float radius = 1.0 - texColor.r;
            
            return mix(vec3(0.0), vec3(1.0), step(radius, dist));
        }

        float aastep(float threshold, float value) {
            float afwidth = 0.7 * length(vec2(dFdx(value), dFdy(value)));
                        
            return smoothstep(threshold - afwidth, threshold + afwidth, value);
        }
        
        void main() {
            vec4 color;

            if(mode == 0) {
                outColor = texture(tex, texCoord);

            } else if (mode == 1) {
                outColor = vec4(vec3(grayscale()), 1.0);

            } else if (mode == 2) {
                color = texture(tex, texCoord);
                float val = color.r;
                float r = float(range) / 100.0;

                if(val > r) {
                    outColor = vec4(1);
                } else {
                    outColor = vec4(vec3(0), 1);
                }

            } else if (mode == 3) { // random
                color = texture(tex, texCoord);
                float pix = color.r;

                if(pix < random(texCoord)) {
                    outColor = vec4(vec3(0), 1);
                } else {
                    outColor = vec4(1);
                }
                //outColor = texture(tex, texCoord) + color;//vec4(luminance,luminance,, 1.0);

            } else if (mode == 4) { // 2x2
                int ditherColor = dither(2.0);
                outColor = vec4(vec3(ditherColor), 1.0);

            } else if (mode == 5) { // 3x3
                int ditherColor = dither(3.0);
                outColor = vec4(vec3(ditherColor), 1.0);

            } else if (mode == 6) { // 4x4
                int ditherColor = dither(4.0);
                outColor = vec4(vec3(ditherColor), 1.0);

            } else if (mode == 7) { // 8x8
                int ditherColor = dither(8.0);
                outColor = vec4(vec3(ditherColor), 1.0);

            } else if (mode == 8) { // 5x5
                int ditherColor = dither(5.0);
                outColor = vec4(vec3(ditherColor), 1.0);

            } else if (mode == 9) { // halftone
                vec3 finalColor = halftone();
                outColor = vec4(finalColor, 1.0);

            }

        }
    </script>
    
    <script src="program.js"></script>
    <script src="app.js"></script>
</body>
</html>